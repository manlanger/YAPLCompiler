/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = true;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(YaplParser)

package yapl;

import yapl.interfaces.*;
import yapl.lib.CompilerMessage;
import yapl.lib.YAPLException;
import yapl.compiler.ParseException;
import yapl.compiler.TokenMgrError;
import yapl.lib.Type;
import yapl.impl.*;
import yapl.types.*;
import java.io.FileOutputStream;
import java.util.List;
import java.util.ArrayList;

public class YaplParser
{
    private static String programName = "";
    private static Symboltable symbolTable = new SymbolTableImpl();
    private static BackendBinSM backend = new BackendMJ();
    private static CodeGen codeGen = new CodeGenImpl(backend);
    private static final String predefinedProcedures = "Procedure void writeint(int i);Procedure void writebool(bool b);Procedure void writeln();Procedure int readint();";

  	public static void main(String args []) throws ParseException
  	{
      	YaplParser parser = null;

      	// init predefined procedures
      	try {
      		parser = new YaplParser(new java.io.StringReader(predefinedProcedures));
      		parser.PreDefinedProcedures();
      	} catch(Throwable e) {
	        System.out.println ("Predefined procedure init failed! " + e.getMessage());
			return;
	    }

	    if(args.length == 0){
	    	System.out.println ("YaplParser: Reading input ...");
	    	parser = new YaplParser(System.in);
	    }
	    else {
	        try {
	          parser.ReInit(new java.io.FileInputStream(args[0]));
	          //System.out.println ("YaplParser: Reading the file " + args[0] + " ..." );
	        } catch(java.io.FileNotFoundException e) {
	        	System.out.println ("ExampleParser: The file " + args[0] + " was not found.");
	        	return;
	        }
	    }

		if (parser == null) {
		  	System.out.println ("Parser initialization failed!");
		} else {
		    try {
		        parser.Start();
		        CompilerMessage.printOK(programName);
		        backend.writeObjectFile(new FileOutputStream(args[1]));
		    }catch (Throwable e)
		    {
		        e.printStackTrace();
                CompilerMessage.printError((CompilerError) e, programName);
		    }
		}
  	}
}

PARSER_END(YaplParser)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

SPECIAL_TOKEN :
{
  <COMMENT: "/*" (~["*"])* "*" (~["*","/"] (~["*"])* "*" | "*")* "/"> |
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
}

/*
Symbols
*/
TOKEN :
{
  < HASH : "#" >
| < PERC : "%" >
| < LEFTPAR : "(" >
| < RIGHTPAR : ")" >
| < MULT : "*" >
| < PLUS : "+" >
| < COMMA : "," >
| < MINUS : "-" >
| < DOT : "." >
| < SLASH : "/" >
| < LESSTHAN : "<" >
| < GREATERTHAN : ">" >
| < LEFTBRACKET : "[" >
| < RIGHTBRACKET : "]" >
| < OR : "Or" >
| < AND : "And" >
| < INT : "int" >
| < NEW : "new" >
| < TRUE : "True" >
| < FALSE : "False" >
| < BOOL : "bool" >
| < IF : "If" >
| <SEMICOLOM : ";" >
| <WHILE : "While" >
| <VOID : "void" >
| <ELSE : "Else" >
| <END : "End" >
| <WRITE : "Write" >
| <PROGRAM : "Program" >
| <DECLARE: "Declare" >
| <EQUALS: "=" >
| <CONST: "Const">
| <PROCEDURE: "Procedure" >
| <BEGIN: "Begin" >
| <RETURN: "Return" >
| <COLOMEQUALS: ":=">
| <DO:"Do">
| <THEN:"Then">
| <ENDIF:"EndIf" >
| <RECORD:"Record" >
| <ENDRECORD:"EndRecord" >
| <ENDWHILE: "EndWhile" >
}

/*
letter = "A" .. "Z" | "a" .. "z" | "_" .
digit = "0" .. "9" .
otherchar = "+" | "-" | "*" | "/" | "." | "," | ";" | ":" | "!"
| "?" | "=" | "#" | "%" | "<" | ">" | "$" | "(" | ")"
| "[" | "]" | "{" | "}" | "\" | "@" | "&" | "^" | "|" .
ident = letter { letter | digit } .
number = digit { digit } .
string = '"' { " " | letter | digit | otherchar } '"' .
*/

TOKEN :
{
  < #LETTER : (["A"-"Z","a"-"z","_"]) >
| < #DIGIT : [ "0"-"9" ] >
| < OTHERCHAR : "+" | "-" | "*" | "/" | "." | "," | ";" | ":" | "!" | "?" | "=" | "#" | "%" | "<" | ">" | "$" | "(" | ")" | "[" | "]" | "{" | "}" | "\\" | "@" | "&" | "^" | "|" >
| < IDENT : < LETTER > ( < LETTER > | < DIGIT > )* >
| < NUMBER : < DIGIT > ( < DIGIT > )* >
| < STRING : "\"" ( " " | < LETTER > | < DIGIT > | < OTHERCHAR > )* "\"" > 
}

/*
RelOp = "<" | "<=" | ">=" | ">" .
EqualOp = "==" | "!=" .
AddOp = "+" | "-" .
MulOp = "*" | "/" | "%" .
*/

Token RelOp() :
{
	Token token;
}
{
  	( token = "<" | token = "<=" | token = ">=" | token = ">" )
  	{
		return token;
	}
}

Token EqualOp() :
{
	Token token;
}
{
  	( token = "==" | token = "!=" )
    {
		return token;
	}
}

Token AddOp() :
{
	Token token;
}
{
  	( token = "+" | token = "-" )
  	{
  		return token;
  	}
}

Token MulOp() :
{
	Token token;
}
{
  	( token = "*" | token = "/" | token = "%" )
  	{
    	return token;
  	}
}

/*
Literal = "True" | "False" | number .
Selector = ( "[" Expr "]" | "." ident ) [ Selector ] .
ArrayLen = "#" ident [ Selector ] .
*/

Attrib Literal() :
{
	Token token;
	Attrib attrib;
	int offset;
}
{
  	<TRUE>
	{
	  	attrib = new AttribImpl(Attrib.Constant, new BoolType(true));
		return attrib;
	}
	
	| <FALSE>
	{
		attrib = new AttribImpl(Attrib.Constant, new BoolType(false));
		return attrib;
	}
		
	| token = <NUMBER>
	{
	 	attrib = new AttribImpl(Attrib.Constant, new IntType(Integer.parseInt(token.image)));
	  	return attrib;
	}
}

Attrib Selector(Attrib a) throws YAPLException:
{
 	Token token2, dot;
	Attrib index = null, attrib = null;
	boolean opensRecordScope = false;
	if(a.getType() instanceof RecordType)
    {
        RecordType rType = (RecordType) a.getType();
        symbolTable.openScope(false);
        symbolTable.setParentSymbol(rType.getRecordSymbol());
        opensRecordScope = true;
    }
}
{
  	( token2 = "[" index = Expr() "]"
  	{
  	  	try { 
			codeGen.arrayOffset(a, index);

			attrib = new AttribImpl(Attrib.ArrayElement, ((ArrayType)a.getType()).getElementType());
			
		} catch (YAPLException e) {
		  	throw new YAPLException("expression before " + token.image + " is not an array type", CompilerError.SelectorNotArray, token);
		}
  	} | dot = "." token2 = <IDENT>
  	{
  		if (!(a.getType() instanceof RecordType)) {
  			throw new YAPLException("expression before " + dot.image + " is not a record type", CompilerError.SelectorNotRecord, token);
  		} else
        {
            Symbol field = symbolTable.lookup(token2.image);
             if(field == null)
             {
                 throw new YAPLException("invalid field "+ token2.image +" of record Stat", CompilerError.InvalidRecordField, token2);
             }
            attrib = new AttribImpl(field.getType());
        }
  	}
  	)
  	[
    attrib = Selector(attrib)
  	{
  	  	//attrib = new AttribImpl(Attrib.ArrayElement, a.getType());
  	} ]
  	{
  	  	if (index != null) { 
	  	  	if (!(index.getType() instanceof IntType)) {
	  		  	throw new YAPLException("array index or dimension is not an integer type", CompilerError.BadArraySelector, token2);
	  		}

	  	}
  	  	if(opensRecordScope)
        {
            opensRecordScope = false;
            symbolTable.closeScope();
        }
        return attrib;
  	}
}

Attrib ArrayLen() throws YAPLException:
{
	Token ht, token;
	Attrib attrib;
	Attrib childAttrib;
}
{
  	ht = "#" token = <IDENT>
  	{
		Symbol symbol = symbolTable.lookup(token.image);
		attrib = new AttribImpl(symbol);
  		
    	if (symbol == null) {
    		throw new YAPLException("identifier " + token.image + " not declared", CompilerError.IdentNotDecl, token);
    	}

    	if (symbol.getKind() == Symbol.Constant) {
    	  	throw new YAPLException("illegal use of constant " + symbol.getName(), CompilerError.SymbolIllegalUse, token);
    	}

    	if (symbol.getKind() == Symbol.Procedure) {
    	  	throw new YAPLException("illegal use of procedure " + symbol.getName(), CompilerError.SymbolIllegalUse, token);
    	}

    	if (!(symbol.getType() instanceof ArrayType)) {
    		throw new YAPLException("expression after " + ht.image + " is not an array type", CompilerError.ArrayLenNotArray, token);
    	}

  	} [ attrib = Selector(attrib) ]
  	{
        if(!(attrib.getType() instanceof ArrayType))
        {
            throw new YAPLException("expression after " + ht.image + " is not an array type", CompilerError.ArrayLenNotArray, token);
        }
  		return codeGen.arrayLength(attrib);
  	}
}

/*
PrimaryExpr = Literal | "(" Expr ")" | ProcedureCall | ident [ Selector ] | ArrayLen .
UnaryExpr = [AddOp] PrimaryExpr .
MulExpr = UnaryExpr { MulOp UnaryExpr } .
AddExpr = MulExpr { AddOp MulExpr } .
RelExpr = AddExpr [ RelOp AddExpr ] .
*/

Attrib PrimaryExpr() throws YAPLException:
{
	Token token;
	Attrib attrib = null;
}
{
	( attrib = Literal()
	{
	  	if (attrib.getType() instanceof IntType) { 
	  		codeGen.loadConstant(((IntType)attrib.getType()).getValue());
	  	} else if (attrib.getType() instanceof BoolType) { 
	  		codeGen.loadConstant(((BoolType)attrib.getType()).getValue());
	  	}
	  	return attrib;
	}
	| "(" attrib = Expr() ")" | LOOKAHEAD(2) attrib = ProcedureCall(true) | token = <IDENT>
  	{
  		Symbol symbol = symbolTable.lookup(token.image);

		if (symbol == null) {
    		throw new YAPLException("identifier " + token.image + " not declared", CompilerError.IdentNotDecl, token);
    	}
    	
  		if (attrib == null) { 
  			attrib = new AttribImpl(symbol);
  		}

  		System.out.println("PrimaryExpr - Symbol: " + symbol.getName() + " is kind of " + symbol.getKind()); 

  		if (symbol.getKind() == Symbol.Constant) {
  		  	if (attrib.getType() instanceof IntType) { 
	  			codeGen.loadConstant(((IntType)attrib.getType()).getValue());
	  		} else if (attrib.getType() instanceof BoolType) { 
	  			codeGen.loadConstant(((BoolType)attrib.getType()).getValue());	
	  		}
  		} else if (symbol.getKind() == Symbol.Variable || symbol.getKind() == Symbol.Parameter) {
  		  	codeGen.loadVariable(symbol);
  		}

    	if (symbol.getKind() == Symbol.Procedure) {
    	  	throw new YAPLException("illegal use of procedure " + symbol.getName(), CompilerError.SymbolIllegalUse, token);
    	}
  	}
  	[ attrib = Selector(attrib) ] | attrib = ArrayLen() )
  	{
  		return attrib;
  	}
}

Attrib UnaryExpr() throws YAPLException:
{
  	Token op = null;
	Attrib attrib;
}
{
  	[ op = AddOp() ] attrib = PrimaryExpr()
  	{
  	  	codeGen.op1(op, attrib);
		return attrib;
  	}
}

Attrib MulExpr() throws YAPLException:
{
	Attrib a1, a2;
	Token op;
}
{
  	a1 = UnaryExpr() ( op = MulOp() a2 = UnaryExpr()
  	{
  		a1 = codeGen.op2(a1, op, a2);
  	} )*
  	{
  		return a1;
  	}
}

Attrib AddExpr() throws YAPLException:
{  
	Attrib a1, a2;
	Token op;
}
{
  	a1 = MulExpr() ( op = AddOp() a2 = MulExpr()
  	{
  		a1 = codeGen.op2(a1, op, a2);
  	} )*
  	{
  		return a1;
  	}
}

Attrib RelExpr() throws YAPLException:
{
  	Attrib a1, a2;
	Token op;
}
{
  	a1 = AddExpr() [ op = RelOp() a2 = AddExpr()
  	{
  		a1 = codeGen.relOp(a1, op, a2);
  	} ]
  	{
  		return a1;
  	}
}

/*
EqualExpr = RelExpr [ EqualOp RelExpr ] .
CondAndExpr = EqualExpr { "And" EqualExpr } .
CreationExpr = "new" NonArrayType { "[" Expr "]" } .
Expr = CondAndExpr { "Or" CondAndExpr } | CreationExpr .
*/

Attrib EqualExpr() throws YAPLException:
{
   	Attrib a1, a2;
	Token op;
}
{
	a1 = RelExpr() [ op = EqualOp() a2 = RelExpr()
	{
		a1 = codeGen.equalOp(a1, op, a2);
	} ]
	{
		return a1;
	}
}

Attrib CondAndExpr() throws YAPLException:
{
	Attrib a1, a2;
	Token token;
}
{
  	a1 = EqualExpr() ( token = "And" a2 = EqualExpr()
  	{
  		codeGen.op2(a1, token, a2);
  	}	
    )*
  	{
  		return a1;
  	}
}

// CreationExpr = "new" NonArrayType { "[" Expr "]" }
Attrib CreationExpr() throws YAPLException:
{
  	Attrib index, attrib;
  	Type type;
  	Token token;
}
{
  	token = "new" type = NonArrayType()
  	( token = "[" index = Expr() "]"
  	{
  		if (!(index.getType() instanceof IntType)) {
  			throw new YAPLException("bad array selector " + index, CompilerError.BadArraySelector, token);
  		}
  		type = new ArrayType(type);
		codeGen.storeArrayDim(0, index);
  		codeGen.allocArray((ArrayType)type);
  	}
  	)*
  	{
        if(type instanceof IntType || type instanceof BoolType)
        {
            throw new YAPLException("invalid type used with 'new'", CompilerError.InvalidNewType, token);
        }
  	  	attrib = new AttribImpl(Attrib.ArrayElement, type);
  		return attrib;
  	}
}

Attrib Expr() throws YAPLException:
{
	Attrib a1, a2;
	Token token;
}
{
  	( a1 = CondAndExpr()  ( token = "Or" a2 = CondAndExpr()
  	{
  		if (!((a1.getType() instanceof BoolType) && (a2.getType() instanceof BoolType)))
  		{
  			throw new YAPLException("illegal operand types for binary operator Or", CompilerError.IllegalOp2Type, token);
  		}

  		codeGen.op2(a1, token, a2);
  	}
  	)* | a1 = CreationExpr())
  	{
  		return a1;
  	}
}

/*
ArgumentList = Expr { "," Expr } .
ProcedureCall = ident "(" [ ArgumentList ] ")" .
*/

List<Attrib> ArgumentList(Symbol procSymbol) throws YAPLException:
{
    Symbol nextSymbol = procSymbol.getNextSymbol();
    Attrib attrib;
    Attrib paraAttrib;
    List<Attrib> args = new ArrayList<Attrib>();
    int counter = 0;
}
{
  attrib = Expr()
  {
    counter++;
    if(nextSymbol == null || nextSymbol.getKind() != Symbol.Parameter)
    {
    	String msg = String.format("argument #%s not applicable to procedure %s", counter, procSymbol.getName());
        throw new YAPLException(msg, CompilerError.ArgNotApplicable, token);
    }
    paraAttrib = new AttribImpl(nextSymbol.getType());
    args.add(attrib);

    if(!paraAttrib.getType().equals(attrib.getType()))
    {
         String msg = String.format("argument #%s not applicable to procedure %s", counter, procSymbol.getName());
         throw new YAPLException(msg, CompilerError.ArgNotApplicable, token);
    }
  }
  ( "," attrib = Expr()
  {
    nextSymbol = nextSymbol.getNextSymbol();
    counter++;
    if(nextSymbol == null || nextSymbol.getKind() != Symbol.Parameter)
        {
        String msg = String.format("argument #%s not applicable to procedure %s", counter, procSymbol.getName());
            throw new YAPLException(msg, CompilerError.ArgNotApplicable, token);
        }
        paraAttrib = new AttribImpl(nextSymbol.getType());
        args.add(attrib);
        if(!paraAttrib.getType().equals(attrib.getType()))
            {
                 String msg = String.format("argument #%s not applicable to procedure %s", counter, procSymbol.getName());
                throw new YAPLException(msg, CompilerError.ArgNotApplicable, token);
            }
  }
  )*
  {
    return args;
  }
}

Attrib ProcedureCall(boolean isFunction) throws YAPLException:
{
	Token token;
	Attrib attrib;
	int paraCount = 0;
	int expectedParams = 0;
	List<Attrib> args = null;
	Symbol symbol;
}
{
  	token = <IDENT>
  	{
		symbol = symbolTable.lookup(token.image);
		attrib = new AttribImpl(symbol.getType());

    	if (symbol == null) {
    		throw new YAPLException("identifier " + token.image + " not declared", CompilerError.IdentNotDecl, token);
    	}

    	if (symbol.getKind() != Symbol.Procedure) {
    	  	throw new YAPLException("illegal use of variable " + token.image, CompilerError.SymbolIllegalUse, token);
    	}

    	if (isFunction && (symbol.getType() instanceof VoidType)) {
		  	throw new YAPLException("using procedure " + symbol.getName() + " (not a function) in expression", CompilerError.ProcNotFuncExpr, token); 
		}
    	paraCount = 0;

    	if(symbol != null)
        {
            Symbol nextSymbol = symbol.getNextSymbol();

            while(nextSymbol != null && nextSymbol.getKind() == Symbol.Parameter)
            {
                nextSymbol = nextSymbol.getNextSymbol();
                expectedParams++;
            }
        }


  	} "(" [ args = ArgumentList(symbol) ] token = ")"
  	{
  	  	if (args != null) { 
			paraCount = args.size();
			Attrib[] argsArray = new Attrib[paraCount];
			argsArray = args.toArray(argsArray);
  	  		codeGen.callProc(symbol, argsArray);
		} else {
		  	paraCount = 0;
		  	codeGen.callProc(symbol, null);
		}
  	 	
  	    if(expectedParams > paraCount)
  	    {
            throw new YAPLException("too few arguments for procedure " + symbol.getName(), CompilerError.TooFewArgs, token);
  	    }// too few arguments for procedure <nam
  		return attrib;
  	}
}

void Assignment() throws YAPLException:
{
	Token token2, token3;
	Attrib attrib, value;
}
{
    token2 = < IDENT >
    {
    	Symbol symbol = symbolTable.lookup(token2.image);
    	System.out.println("Got " + symbol.getName() + " and scope " + symbol.isGlobal());
    	attrib = new AttribImpl(symbol);

    	if (symbol == null) {
    		throw new YAPLException("identifier " + token2.image + " not declared", CompilerError.IdentNotDecl, token2);
    	}

    	if (symbol.getKind() != Symbol.Variable && symbol.getKind() != Symbol.Parameter) {
    		throw new YAPLException("illegal use of " + symbol.getKindString() + " " + symbol.getName(), CompilerError.SymbolIllegalUse, token2);
    	}

    }
    [ attrib = Selector(attrib) ] token3 = ":=" value = Expr()
    {
     	try { 
      		codeGen.assign(attrib, value);
     	} catch (YAPLException e) {
     	  	throw new YAPLException(e.getMessage(), e.errorNumber(), token3);
     	}
    }
}

void IfStatement() throws YAPLException:
{
	Attrib attrib;
	Token token;
	String elseLabel, endLabel;
}
{
    token = "If" attrib = Expr()
    {
    	if (!(attrib.getType() instanceof BoolType)) {
    	  	throw new YAPLException("Condition not boolean expression", CompilerError.CondNotBool, token);
    	}

    	elseLabel = codeGen.newLabel();
		codeGen.branchIfFalse(attrib, elseLabel);
    	
    } "Then" StatementList()
    {
      	endLabel = codeGen.newLabel();
	  	codeGen.jump(endLabel);
		codeGen.assignLabel(elseLabel);
    } [ "Else" StatementList() ] "EndIf"
    {
      	codeGen.assignLabel(endLabel);
    }
}

void WhileStatement() throws YAPLException:
{
	Attrib attrib;
	Token token;
	String loopLabel, endLabel;
}
{
    token = "While"
    {
      	loopLabel = codeGen.newLabel();
	  	codeGen.assignLabel(loopLabel);
    } attrib = Expr()
    {
      	endLabel = codeGen.newLabel();
		codeGen.branchIfFalse(attrib, endLabel);
		
    	if (!(attrib.getType() instanceof BoolType)) {
    	  	throw new YAPLException("Condition not boolean expression", CompilerError.CondNotBool, token);
    	}
    }
    "Do" StatementList() "EndWhile"
    {
	  	codeGen.jump(loopLabel);
	  	codeGen.assignLabel(endLabel);
	}
}

boolean ReturnStatement() throws YAPLException:
{
	Attrib attrib = null;
	Token token2;
	Token token3 = token;
	Symbol procedure = symbolTable.getNearestParentSymbol(Symbol.Procedure);
    Attrib procAttrib = null;
    if(procedure != null)
    {
          procAttrib =   new AttribImpl(procedure.getType());
    }
}
{
    token2 = "Return" [ attrib = Expr()
    {
        token3 = token;
    	if (attrib.getType() instanceof VoidType || procAttrib != null && procAttrib.getType() instanceof VoidType) {
    	  	throw new YAPLException("Return value must not be void", CompilerError.IllegalRetValProc, token2);
    	} else if(procAttrib == null && !(attrib.getType() instanceof VoidType))
        {
            throw new YAPLException("illegal return value in main program", CompilerError.IllegalRetValMain, token3);
        }

    }
    ]
    {
        if(procAttrib != null)
        {
            if(attrib != null )
            {
                if(!procAttrib.getType().equals(attrib.getType()))
                {
                    throw new YAPLException("returning none or invalid type from function " + procedure.getName(), CompilerError.InvalidReturnType, token3);
                }
            }else if(!(procAttrib.getType() instanceof VoidType))
            {
                throw new YAPLException("returning none or invalid type from function " + procedure.getName(), CompilerError.InvalidReturnType, token);
            }
        }

		procedure.setReturnSeen(true);
		codeGen.returnFromProc(procedure, attrib);

        return true;
    }
}

void WriteStatement() throws YAPLException:
{
	Token token;
}
{
    "Write" token = <STRING>
    {
		codeGen.writeString(token.image);     
    }
}

boolean Statement() throws YAPLException:
{
    boolean isReturnStatement = false;
    }
{
    (IfStatement() | WhileStatement() | isReturnStatement = ReturnStatement() | WriteStatement() | LOOKAHEAD(2) Assignment() |  LOOKAHEAD(2) ProcedureCall(false) | Block(false))
    {
        return isReturnStatement;
    }
}

boolean StatementList() throws YAPLException:
{
    boolean lastStatementIsReturnStatement = false;
    }
{
    ( lastStatementIsReturnStatement = Statement() ";" )*
    {
        return lastStatementIsReturnStatement;
    }
}

void Block(boolean endsWithReturn) throws YAPLException:
{
    boolean lastStatementIsReturnStatement = false;
    }
{
    {
    	symbolTable.openScope(false);
    }
    [ Decl(false) ] "Begin" lastStatementIsReturnStatement = StatementList()
    "End"
    {
        if(endsWithReturn && !lastStatementIsReturnStatement)
        {
            Symbol procedure = symbolTable.getNearestParentSymbol(Symbol.Procedure);
            throw new YAPLException("missing Return statement in function " + procedure.getName(), CompilerError.MissingReturn, token);
        }
    	symbolTable.closeScope();
    }
}

/*
NonArrayType = "int" | "bool" | ident .
*/

Type NonArrayType() throws YAPLException:
{
	Token token;
	Attrib attrib = null;
}
{
	( "int"
	{
		return new IntType();
	} | "bool"
	{
	  	return new BoolType();
	}
	| token = <IDENT>
	{
		Symbol symbol = symbolTable.lookup(token.image);
		attrib = new AttribImpl(symbol);
  		
    	if (symbol == null) {
    		throw new YAPLException("identifier " + token.image + " not declared", CompilerError.IdentNotDecl, token);
    	}

    	if (symbol.getKind() == Symbol.Constant) {
    		throw new YAPLException("illegal use of constant " + symbol.getName(), CompilerError.SymbolIllegalUse, token);
    	}
    	
    	return attrib.getType();
	}
	)
}

Type Type() throws YAPLException:
{
	Type type;
}
{
    type = NonArrayType() ( "[" "]"
    {
    	type = new ArrayType(type);
    } )*
    {
    	return type;
    }
}

Type ReturnType() throws YAPLException:
{
	Type type;
}
{
    "void"
    {
    	return new VoidType();
    } | type = Type()
    {
    	return type;
   	}
}

void ConstDecl(boolean isGlobal) throws YAPLException:
{
  	Symbol symbol;
	Token token;
	Attrib attrib;
}
{
    "Const" token = <IDENT>
    {
      	symbol = new SymbolImpl(token.image, Symbol.Constant);
      	
    	try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	} 
    }
    "=" attrib = Literal()
    {
    	symbol.setType(attrib.getType());
    	symbol.setGlobal(isGlobal);
    	System.out.println("Symbol " + symbol.getName() + " is a Constant");
    	symbol.setKind(Symbol.Constant);
    } ";"
}

void VarDecl(boolean isGlobal) throws YAPLException:
{
	Token token;
	Symbol symbol;
	Type type;
}
{
    type = Type() token = <IDENT>
    {
      	symbol = new SymbolImpl(token.image, Symbol.Variable);
      	symbol.setType(type);
      	symbol.setGlobal(isGlobal);
      	System.out.println("Symbol " + symbol.getName() + " is a Variable.");
      	symbol.setKind(Symbol.Variable);
    	codeGen.allocVariable(symbol);

      	try {
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	} 
    }
    ( "," token = <IDENT>
    {
      	symbol = new SymbolImpl(token.image, Symbol.Variable);
    	symbol.setType(type);
    	symbol.setGlobal(isGlobal);
    	System.out.println("Symbol " + symbol.getName() + " is a Variable.");
    	symbol.setKind(Symbol.Variable);
    	codeGen.allocVariable(symbol);
    	
		try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	}
    }
    )* ";"
}

void TypeDecl() throws YAPLException:
{
	Token token;
	Type type;
}
{
    "Record" token = <IDENT>
    {
    	Symbol symbol = new SymbolImpl(token.image, Symbol.Typename);
    	type = new RecordType(symbol);
    	symbol.setType(type);

      	try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	}

    	symbolTable.openScope(false);
      	symbolTable.setParentSymbol(symbol);
    }
    VarDecl(false) ( VarDecl(false) )* "EndRecord"
    {
    	symbolTable.closeScope();
    } ";"
}

void Decl(boolean isGlobal) throws YAPLException:
{}
{
    "Declare" ( ConstDecl(isGlobal) | VarDecl(isGlobal) | TypeDecl() )*
}

Symbol FormalParam() throws YAPLException:
{
  	Type type;
	Token token;
	Symbol symbol;
}
{
    type = Type() token = <IDENT>
    {
    	symbol = new SymbolImpl(token.image, Symbol.Parameter);
    	symbol.setGlobal(false);
    	symbol.setType(type);
    	
		try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	}

    	return symbol;
    }
}

Symbol FormalParamList() throws YAPLException:
{
  	Symbol first, next, temp = null;
}
{
    first = FormalParam() ( "," next = FormalParam()
    {
      	if (temp == null) { 
      		first.setNextSymbol(next);
      	} else {
      	  	temp.setNextSymbol(next);
      	}

      	temp = next;
    } )*
    {
      	return first;
    }
}

Attrib Procedure() throws YAPLException :
{
  	Attrib attrib;
  	Type returnType;
  	Token start_proc;
  	Token end_proc;
  	Symbol symbol;
  	Symbol first = null;
  	boolean isVoidReturnType;
}
{
    "Procedure" returnType = ReturnType() start_proc = <IDENT>
    {
    	symbol = new SymbolImpl(start_proc.image, Symbol.Procedure);
		attrib = new AttribImpl(returnType);
		symbol.setType(returnType);
		isVoidReturnType = returnType instanceof VoidType;
    	
    	try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	}

    	symbolTable.openScope(false);
    	symbolTable.setParentSymbol(symbol);
    }

     "(" [ first = FormalParamList() ]
     {
       	System.out.println("Symbol " + symbol.getName() + " is a Procedure.");
       	symbol.setKind(Symbol.Procedure);
       	symbol.setNextSymbol(first);
       	codeGen.enterProc(symbol);
     }
     ")" Block(!isVoidReturnType) end_proc = <IDENT>
     {
       	codeGen.exitProc(symbol);
       	if (!start_proc.image.equals(end_proc.image))
       		throw new YAPLException("End " + end_proc.image + " does not match Procedure " + start_proc.image, CompilerError.EndIdentMismatch, end_proc);
     	symbolTable.closeScope();
     } ";"
     {
     	return attrib;
     }
}

void Program() throws YAPLException:
{
 	Token start_prog;
  	Token end_prog;
  	Symbol symbol;
}
{
    "Program" start_prog = <IDENT>
    {
    	programName = start_prog.toString();
    	symbol = new SymbolImpl(start_prog.image, Symbol.Program);
		symbolTable.openScope(true);
    	symbolTable.setParentSymbol(symbol);
    }
    (Decl(true)
    {
      	
    } | Procedure())*
    "Begin"
    {
      	codeGen.enterProc(null);
    }
    StatementList()
    "End" end_prog = <IDENT>
    {
    	if (!start_prog.image.equals(end_prog.image))
       		throw new YAPLException("End " + end_prog.image + " does not match Program " + start_prog.image, CompilerError.EndIdentMismatch, end_prog);
       	codeGen.exitProc(null);
    } "."
}

void PreDefinedProcedures() throws YAPLException:
{}
{
  	(PreDefinedProcedure())*
}

Attrib PreDefinedProcedure() throws YAPLException:
{
  	Type type;
  	Attrib attrib;
	Token token;
	Symbol symbol;
} 
{
	"Procedure" type = ReturnType() token = <IDENT>
    {
    	symbol = new SymbolImpl(token.image, Symbol.Procedure);
		attrib = new AttribImpl(type);
		symbol.setType(type);
    	symbolTable.openScope(true);
    	
    	try { 
    		symbolTable.addSymbol(symbol);
    	} catch (YAPLException e) {
    	  	throw new YAPLException(e.getMessage(), CompilerError.SymbolExists, token);
    	}

    	symbolTable.setParentSymbol(symbol);
    }
    "(" [ FormalParamList() ] ")"
    ";"
    {
    	return attrib;
    }
}

void Start() throws YAPLException:
{}
{
    Program() <EOF>
}